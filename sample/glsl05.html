<!DOCTYPE html>
<html>

<head>
  <title>glsl05</title>
</head>

<body>
  <script type="text/javascript"
    src="https://rawgit.com/patriciogonzalezvivo/glslCanvas/master/dist/GlslCanvas.js"></script>

  <!-- https://thebookofshaders.com/06/?lan=kr -->
  <script type="x-shader/x-fragment">
    #ifdef GL_ES
    precision mediump float;
    #endif

    uniform vec2 u_resolution;
    uniform float u_time;

    // 두개 색을 정의
    vec3 colorA = vec3(0.149,0.141,0.912);
    vec3 colorB = vec3(1.000,0.833,0.224);

    void main() {
        vec3 color = vec3(0.0);

        // 시간에 따른 sin 값을 mix 수치에 사용
        float pct = abs(sin(u_time));

        // Mix uses pct (a value from 0-1) to
        // mix the two colors: (1-pct) * colorA + pct * colorB
        // pct가 0인 경우는 colorA라 되고 pct가 1이되면 colorB로 바뀜
        color = mix(colorA, colorB, pct);

        gl_FragColor = vec4(color,1.0);
    }
  </script>

  <script type="x-shader/x-fragment">
    #ifdef GL_ES
    precision mediump float;
    #endif

    #define PI 3.14159265359

    uniform vec2 u_resolution;
    uniform vec2 u_mouse;
    uniform float u_time;

    // 2개 색을 정의
    vec3 colorA = vec3(0.149,0.141,0.912);
    vec3 colorB = vec3(1.000,0.833,0.224);

    // pct-0.01 <= st.y <= pct+0.1 사이에서 0~1이 됨. 그외의 범위는 0이됨.
    float plot (vec2 st, float pct){
      return  smoothstep( pct-0.01, pct, st.y) -
              smoothstep( pct, pct+0.01, st.y);
    }

    void main() {
        // 좌표 정규화
        vec2 st = gl_FragCoord.xy/u_resolution.xy;
        vec3 color = vec3(0.0); // black

        // x좌표에 따른 그라데이션 색
        vec3 pct = vec3(st.x);

        //pct.r = smoothstep(0.0,1.0, st.x); // x좌표 범위에서 0.0~1.0으로 부드럽게 함.
        //pct.g = sin(st.x*PI);  // x좌표로 PI값 범위게 180도 까지 sin값 사용
        //pct.b = pow(st.x,0.5); // x좌표에 0.5승으로 root값을 구함.

        // pct가 0인 경우 colorA가 되며 0이 되면 colorB로 변함.
        color = mix(colorA, colorB, pct);

        // Plot transition lines for each channel
        // vec3(st.x)에 의해 gray이므로 rgb가 동일한 값으로 구성됨.
        color = mix(color,vec3(1.0,0.0,0.0),plot(st,pct.r));
        color = mix(color,vec3(0.0,1.0,0.0),plot(st,pct.g));
        color = mix(color,vec3(0.0,0.0,1.0),plot(st,pct.b));

        gl_FragColor = vec4(color,1.0);
    }
  </script>
  <script type="x-shader/x-fragment">
    #ifdef GL_ES
    precision mediump float;
    #endif

    uniform vec2 u_resolution;
    uniform float u_time;

    // HSB 색 채널
    vec3 rgb2hsb( in vec3 c ){
        vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
        vec4 p = mix(vec4(c.bg, K.wz),
                    vec4(c.gb, K.xy),
                    step(c.b, c.g));
        vec4 q = mix(vec4(p.xyw, c.r),
                    vec4(c.r, p.yzx),
                    step(p.x, c.r));
        float d = q.x - min(q.w, q.y);
        float e = 1.0e-10;
        return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)),
                    d / (q.x + e),
                    q.x);
    }

    //  Function from Iñigo Quiles
    //  https://www.shadertoy.com/view/MsS3Wc
    vec3 hsb2rgb( in vec3 c ){
        vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                                6.0)-3.0)-1.0,
                        0.0,
                        1.0 );
        rgb = rgb*rgb*(3.0-2.0*rgb);
        return c.z * mix(vec3(1.0), rgb, c.y);
    }

    void main(){
        vec2 st = gl_FragCoord.xy/u_resolution;
        vec3 color = vec3(0.0);

        // We map x (0.0 - 1.0) to the hue (0.0 - 1.0)
        // And the y (0.0 - 1.0) to the brightness
        color = hsb2rgb(vec3(st.x,1.0,st.y));

        gl_FragColor = vec4(color,1.0);
    }
  </script>
  <script type="x-shader/x-fragment">
    #ifdef GL_ES
    precision mediump float;
    #endif

    #define TWO_PI 6.28318530718

    uniform vec2 u_resolution;
    uniform float u_time;

    //  Function from Iñigo Quiles
    //  https://www.shadertoy.com/view/MsS3Wc
    vec3 hsb2rgb( in vec3 c ){
        vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                                6.0)-3.0)-1.0,
                        0.0,
                        1.0 );
        rgb = rgb*rgb*(3.0-2.0*rgb);
        return c.z * mix( vec3(1.0), rgb, c.y);
    }

    void main(){
        vec2 st = gl_FragCoord.xy/u_resolution;
        vec3 color = vec3(0.0);

        // Use polar coordinates instead of cartesian
        vec2 toCenter = vec2(0.5)-st;
        float angle = atan(toCenter.y,toCenter.x);
        float radius = length(toCenter)*2.0;

        // Map the angle (-PI to PI) to the Hue (from 0 to 1)
        // and the Saturation to the radius
        color = hsb2rgb(vec3((angle/TWO_PI)+0.5,radius,1.0));

        gl_FragColor = vec4(color,1.0);
    }
  </script>
  <script>
    function appendCanvas(source) {
      var canvas = document.createElement('canvas');
      var sandbox = new GlslCanvas(canvas);
      sandbox.load(source);
      document.body.append(canvas);
    }

    var fragments = document.querySelectorAll('script[type="x-shader/x-fragment"]')
    Array.prototype.forEach.call(fragments, function (it) {
      var source = it && it.innerHTML;
      source && appendCanvas(source);
    });
  </script>
</body>

</html>